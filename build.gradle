apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'versions'
apply from: "./libraries.gradle"
apply from: "./idea_settings.gradle"

defaultTasks 'build'

def defaultSourceCompatibility = '1.7'
def defaultTargetCompatibility = '1.7'

ext {
    javaModules = ['common', 'client', 'server', 'java-client', 'performance-tests', 'statistics']
}

def javaProjects() {
    subprojects.findAll { project -> javaModules.contains(project.name) }
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
}

configure(javaProjects()) {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'findbugs'
    apply plugin: 'java'
    apply plugin: 'rebel'
    //apply plugin: 'project-report'

    sourceCompatibility = defaultSourceCompatibility
    targetCompatibility = defaultTargetCompatibility

    group = 'se.cygni'
    version = '1.1.21-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    jar.dependsOn('generateRebel')

    findbugs {
        toolVersion = "2.0.1"
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    findbugsMain {
        reports {
            html {
                enabled = false
            }

            xml.enabled = !html.enabled
        }
    }

    processResources {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version])
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

buildscript {
    repositories {
        mavenCentral()

        mavenRepo(name: 'zt-public-snapshots',
                url: 'http://repos.zeroturnaround.com/nexus/content/groups/zt-public/')
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:0.5.8'
        classpath 'org.zeroturnaround:gradle-jrebel-plugin:1.0.3-SNAPSHOT'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.3'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

configurations.all {
    exclude(group: 'commons-logging')
}

project('common') {
    
    dependencies {
        compile(libraries.spring_context)
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.commons_math)
        compile(libraries.jackson_core_lgpl)
        compile(libraries.jackson_mapper_lgpl)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_basic)
    }
}

project('client') {

    dependencies {
        compile(project(':common'))
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.netty)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_basic)
    }
}

project('server') {
    apply plugin: 'war'
    apply plugin: 'cargo'
    
    def jettyLoggingDir = "$buildDir/logs"

    dependencies {
        compile(project(':common'))
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.netty)
        compile(libraries.jopt_simple)
        compile(libraries.guava)
        compile(libraries.joda_time)

        compile(libraries.spring_core)
        compile(libraries.spring_expression)
        compile(libraries.spring_beans)
        compile(libraries.spring_context)
        compile(libraries.spring_context_support)
        compile(libraries.spring_web)
        compile(libraries.spring_security_web)
        compile(libraries.spring_security_config)
        compile(libraries.spring_webmvc)
        compile(libraries.cglib)
        compile(libraries.tiles_jsp)
        providedCompile(libraries.jstl)
        providedCompile(libraries.servlet)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_full)

        def cargoVersion = '1.3.3'
        cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
                "org.codehaus.cargo:cargo-ant:$cargoVersion"

    }

    // Project Config
    task initConfig(type:Copy) {
        from("$projectDir/config") {
            include '**/*.xml'
            expand(projectDir: projectDir)
        }
        into "$buildDir/config"

    }
    processResources.dependsOn(initConfig)

    task initLogDir << {
        println("Trying to create jetty logging dir: $jettyLoggingDir")
        def jLogDir = new File(jettyLoggingDir)
        println("jetty logging dir creation succeeded: " + jLogDir.mkdirs())
    }

    cargo {
        containerId = 'jetty8x'

        def rebelHome = System.env.REBEL_HOME
        def jvmArgz   = '-Xdebug -Xrunjdwp:transport=dt_socket,address=5555,server=y,suspend=n -XX:MaxPermSize=512m'

        if (rebelHome) {
            jvmArgz = "-noverify -javaagent:'$rebelHome/jrebel.jar' -Drebel.mustache_plugin=true -Drebel.log=true " + jvmArgz
        }
        else {
            println 'REBEL_HOME not found, cargo will not use jRebel java agent'
        }

        local {
            configFile {
                file  = file("$buildDir/config/jetty-logging.xml")
                toDir = file('etc') 
            }

            output   = file("$jettyLoggingDir/startup.log")
            log      = file("$jettyLoggingDir/cargo.log")
            logLevel = 'medium'
            jvmArgs  = jvmArgz
            installer {
                installUrl = 'http://repo1.maven.org/maven2/org/eclipse/jetty/jetty-distribution/8.1.9.v20130131/jetty-distribution-8.1.9.v20130131.zip'
                //installUrl = 'http://repo2.maven.org/maven2/org/eclipse/jetty/jetty-distribution/9.0.0.RC2/jetty-distribution-9.0.0.RC2.zip'
                downloadDir = file("$buildDir/download")
                extractDir  = file("$buildDir/extract")
            }
        }

        deployable {
            context = '/'
        }

        deployable {
            file =  file("$projectDir/build/libs/$name-$version" + ".war")
            context = "$name"
        }

        deployable {
            file =  file("frontend/build/libs/frontend.war")
            context = "frontend"
        }
    }

    war.dependsOn('generateRebel')
    
    project.afterEvaluate {
        cargoRunLocal.dependsOn(initLogDir)
        cargoRunLocal.dependsOn(':server:war')
    }
}

project('frontend') {
    apply plugin: 'war'
    apply plugin: 'rebel'

    war.dependsOn('generateRebel')
}

project('client-implementations:java-client') {

    dependencies {
        compile(project(':common'))
        compile(project(':client'))
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.netty)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_basic)
    }
}

project('labs:performance-tests') {

    dependencies {
        compile(project(':common'))
        compile(project(':client'))
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.netty)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_basic)
    }
}

project('labs:statistics') {

    dependencies {
        compile(project(':common'))
        compile(project(':client'))
        compile(libraries.commons_collections)
        compile(libraries.commons_io)
        compile(libraries.commons_lang)
        compile(libraries.netty)
        compile(libraries.trove4j)

        compile(libraries_grouped.logging)

        testCompile(libraries_grouped.test_basic)
    }
}

